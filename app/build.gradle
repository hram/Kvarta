buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName] != null ? props[propName] : "\"define config field: " + propName + "\""
    } else {
        return "\"define config file local.properties\"";
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "hram.kvarta"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 4
        versionName "1.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "tsgid", getProps("tsgid")
        buildConfigField "String", "accountid", getProps("accountid")
        buildConfigField "String", "password", getProps("password")
        buildConfigField "boolean", "crashlyticsEnabled", 'false'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "crashlyticsEnabled", 'true'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.3.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile "com.squareup.okhttp3:okhttp-urlconnection:3.3.1"
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.github.johnkil.android-robototextview:robototextview:2.4.2'
    compile 'com.getbase:floatingactionbutton:1.10.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true;
    }
    compile 'droidkit:droidkit:5.1.4'
    provided 'droidkit:droidkit-processor:5.1.2'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
}
